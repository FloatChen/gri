# Draw version-history tree
set font size 12
.x. = 5				# cm
.y. = 3				# cm
.fs. = {rpn ..fontsize.. pttocm} # height of font, in cm
.space_below. = {rpn .fs. 6 /}	# whitespace below a version name, in cm
.space_above. = {rpn 0 .fs. 6 / -} # whitespace above
.vsn_length. = {rpn .fs. 3 *}	# length of vertical inter-version line
set arrow size 0.1

`version \number \change1 \change2 \change3'
{
    draw line from .x. .y. to .x. {rpn .y. .vsn_length. +} cm
    set font size {rpn ..fontsize.. 0.75 *}
    draw label "  <\.word4.>" at {rpn .x. "M" width +} {rpn .y. .space_below. +} cm
    set font size {rpn ..fontsize.. 0.75 /}
    draw label "\.word3." at {rpn .x. "M" width +} {rpn .y. .space_below. .fs. + +} cm
    draw label "\.word2." at {rpn .x. "M" width +} {rpn .y. .space_below. .fs. 2 * + +} cm
    .y. += {rpn .vsn_length. .space_below. +}
    draw label "\.word1." centered at .x. .y. cm
    .y. += {rpn .fs. .space_above. +}
}
`from \version'
{
    draw arrow from {rpn .x. .fs. 6 * -} {rpn .y. .fs. 2 / +} to {rpn .x. .fs. - } {rpn .y. .fs. 2 / +} cm
    draw label "\.word1." at {rpn .x. .fs. 5 * -} {rpn .y. .fs. 1.5 / +} cm
}
`name \text'
{
    set font size {rpn ..fontsize.. 1.5 *}
    draw label "\.word1." at .x. .y. cm
    .y. += {rpn .fs. 2 *}
    set font size {rpn ..fontsize.. 1.5 /}
}

name "Version diagram for Gri 2.4.x (stable stream)"
from "2.3.7"
version "2.4.0" "Based on development version 2.3.7" "and stable version 2.2.6." "2000-Jan-05"

# bug reported by Ivo Alxneit.
version "2.4.1" "Remove bug in `convert image to grid' which made " "contouring impossible in the constructed grid." "2000-Jan-31"

# bug reported by Peter Galbraith.
version "2.4.2" "Remove bug in `convert grid to image' which made " "gave images a patchy appearance for coarse grids." "2000-Mar-25"
