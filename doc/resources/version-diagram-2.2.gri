# Draw version-history tree
set font size 12
.x. = 5				# cm
.y. = 3				# cm
.fs. = {rpn ..fontsize.. pttocm} # height of font, in cm
.space_below. = {rpn .fs. 6 /}	# whitespace below a version name, in cm
.space_above. = {rpn 0 .fs. 6 / -} # whitespace above
.vsn_length. = {rpn .fs. 3 *}	# length of vertical inter-version line
set arrow size 0.1

`version \number \change1 \change2 \change3'
{
    draw line from .x. .y. to .x. {rpn .y. .vsn_length. +} cm
    draw label "\.word4." at {rpn .x. "M" width +} {rpn .y. .space_below. +} cm
    draw label "\.word3." at {rpn .x. "M" width +} {rpn .y. .space_below. .fs. + +} cm
    draw label "\.word2." at {rpn .x. "M" width +} {rpn .y. .space_below. .fs. 2 * + +} cm
    .y. += {rpn .vsn_length. .space_below. +}
    draw label "\.word1." centered at .x. .y. cm
    .y. += {rpn .fs. .space_above. +}
}
`from \version'
{
    draw arrow from {rpn .x. .fs. 6 * -} {rpn .y. .fs. 2 / +} to {rpn .x. .fs. - } {rpn .y. .fs. 2 / +} cm
    draw label "\.word1." at {rpn .x. .fs. 5 * -} {rpn .y. .fs. 1.5 / +} cm
}
`name \text'
{
    set font size {rpn ..fontsize.. 1.5 *}
    draw label "\.word1." at .x. .y. cm
    .y. += {rpn .fs. 2 *}
    set font size {rpn ..fontsize.. 1.5 /}
}

name "Version diagram for Gri 2.2.x (stable stream)"
from "2.1.21"
version "2.2.0" "Based on development version 2.1.21." "First debian release." ""
version "2.2.1" "Don't create postscript file if no drawing was done." "Fix image error in top-right pixel." "Debian: properly locate `netcdf' library if it exists."
version "2.2.2" "Documentation: fix spelling errors and a few cross-references." "" ""
version "2.2.3" "Fix bug in word-of-synonym (e.g.'\\\\[0]mysyn') syntax." "" ""

from "2.3.1"
version "2.2.4" "Add support for ISO-latin-1 fonts (i.e. accented letters)." "Based on development version 2.3.1." ""
version "2.2.5" "Fix bug in rpn calculations that" "prevented raising to negative power." ""

version "2.2.6" "Improve aesthetics of web-based manual." "" ""

version "2.2.7" "Add `set input data separator'" "" ""

#draw arrow from 1 1 to 20 20 cm
