# Draw a "tab bar" for navigating web pages.  The output
# is a first approximation to the HTML code for navigating this
# tab bar.  The multi-page postscript output must be split and
# chopped into images outside of Gri.

`tab "label" .highlight.'
Draw a tab for a navigation bar.  It will appear to the right of any
previously drawn tabs, and it will be drawn in a highlighted
colour if  .highlight. equals 1.
{
    \name = {rpn "\.word1." "s:/.*::" sed}
    \url  = {rpn "\.word1." "s:.*/::" sed}
    set x axis 0 10
    set y axis 0 10
    draw axes none
    .right. = {rpn .left. "\name" width + .space_between_tabs. 3 * +}
    if {rpn \.word2. 1 ==}
	set color \foreground
    else
	set color \background
    end if
    draw polygon filled \
	.left. 0 \
	.left. {rpn .h. .chamfer. -} \
	{rpn .left.  .chamfer. +} .h. \
	{rpn .right. .chamfer. -} .h. \
	.right.  {rpn .h. .chamfer. -} \
	.right.  0 \
	.left. 0
    set color \text
    draw label "\name" centered at \
	{rpn .left. .right. + 2 /} \
	{rpn "M" ascent 0.5 *}
    sprintf \xl "%3.0f" {rpn .left.                 cmtopt 0.5 + floor}
    sprintf \xr "%3.0f" {rpn .right.                cmtopt 0.5 + floor}
    sprintf \yb "%3.0f" 0
    sprintf \yt "%3.0f" {rpn .h.                    cmtopt 0.5 + floor}
    if {rpn .hi. 1 ==}
	show "  <area shape=\"rect\" coords=\"\xl,\yb,\xr,\yt\" href=\"\url\">"
    end if
    .left. = {rpn .right. .space_between_tabs. +}
}
`underline'
Draw a region below the tabs, which gives the illusion
that the highlighted tab is in front of the other tabs.
{
    .thickness. = {rpn "M" ascent 1.5 /}
    set color \foreground
    set line width rapidograph 6x0
    draw box filled 0 {rpn 0 .thickness. -} .right. 0
}
`tabs ...'
Draw a set of tabs, on subsequent pages which highlight
subsequent tags.  The arguments are the tag labels
{
    show "<!-- First-guess at HTML code to navigate this -->"
    show "<map name=\"navigate_tabs\">"
    \background = "hsb 0.136 0.318 0.969"
    \foreground = "hsb 0.136 1 1"
    \text       = "rgb 0.000 0.000 0.000"
    .space_between_tabs. = {rpn "M" ascent 0.33 *}
    .chamfer. = {rpn "M" ascent 0.50 *} 
    .h. = {rpn "M" ascent 1.8 * } # height of written part of navbar
    .left. = 0
    .hi. = 1
    while {rpn .hi. \.words. >}
	.left. = 0
	.i. = 1
	while {rpn .i. \.words. >}
	    if {rpn .i. .hi. ==}
		tab {rpn .i. wordv} 1
	    else
		tab {rpn .i. wordv} 0
	    end if
	.i. += 1
	end while
	.hi. += 1
	underline
	if {rpn .hi. \.words. >}
	    new page
	end if
    end while
    show "</map>"
    show "<img border=0 src=\"FILL_IN_IMAGE_NAME\" align=top usemap=#navigate_tabs>"
    show ""
    show "*** next step: chop the pages of postscript file into separate ***"
    show "*** images, and then edit the above HTML to name the images    ***"
}

# Main 
set font size 16
tabs Main/main.php News/news.php Docs/docs.php Download/download.php Bugs/bugs.php Contact/contact.php Developers/developers.php
