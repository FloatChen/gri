show "doc/tst_suite/tst_rpn.gri     ..." ...

rpnfunction same - abs 1e-10 >		# tests if two numbers match to 1e-10

# Arithmetic
.a. = 0
assert {rpn .a. !}			" failed test 1"
.a. += 1
assert .a.				" failed test 2"
.a. += 1
assert {rpn .a. 2 same}			" failed test 3"
.a. *= 2
assert {rpn .a. 4 same}			" failed test 4"
.a. /= 4
assert {rpn .a. 1 same}			" failed test 5"

# Logs, powers
.a. _= 10
assert {rpn .a. 0 same}			" failed test 6"
.a. = 2
.a. ^= 8
assert {rpn .a. 256 same}		" failed test 7"
assert {rpn -2 4 power 16 ==}           " failed test 8"
assert {rpn -2 3 power -8 ==}           " failed test 9"
assert {rpn -2 2 power  4 ==}           " failed test 10"
assert {rpn  2 3 power  8 ==}           " failed test 11"


# String operations
\a = {rpn "file" ".dat" strcat}
assert {rpn "\a" "file.dat" ==}		" failed test 12"
\sentence = "This sentence has five words"
\w1 = word 0 of "\sentence "
assert {rpn "\w1" "This" ==}		" failed test 13"
\w2 = word 1 of "\sentence "
assert {rpn "\w2" "sentence" ==}	" failed test 14"

# Q: will the below work on all OS?????
# NB. better to switch with something more universal
\six = system "date | wc | awk '{print $2}'"
assert {rpn \six 6 ==}			" failed test 15"

# Sine, etc.
# ----------- FILL IN LATER -----------------

# Stack operations (push, pop, exch)
# ----------- FILL IN LATER -----------------


# Pointers (added version 2.6.0)
.v. = 10
\vptr = &.v.
assert {rpn 10 *\vptr ==} " variable-pointer error 16.1"
*\vptr = 20
assert {rpn 20 *\vptr ==} " variable-pointer error 16.2"
\s = "HI"
\sptr = &\s
assert {rpn "HI" "*\sptr" ==} " synonym-pointer error 16.3"
*\sptr = "BYE"
assert {rpn "BYE" "*\sptr" ==} " synonym-pointer error 16.4"

`double &.value.'
{
    *\.word1. = {rpn *\.word1. 2 *}
}
.x. = 10
double &.x.
assert {rpn .x. 20 ==} " failed test 16.5"

`prepend_dat &\filename'
{
    *\.word1. = "mydirectory/*\.word1."
}
\filename = "test.dat"
prepend_dat &\filename
assert {rpn "\filename" "mydirectory/test.dat" ==} " failed test 16.6"

show " passed"
